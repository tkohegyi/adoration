description= 'AdorApp'

task docs(type: Javadoc) {
    destinationDir = file("$buildDir/docs/all")
    options.memberLevel = JavadocMemberLevel.PRIVATE
    for (subproject in project.subprojects) {
        if (subproject.plugins.hasPlugin(JavaPlugin)){
            source += files(subproject.sourceSets.main.java)
            classpath += files(subproject.sourceSets.main.compileClasspath)
        }
    }
}

task checkstyle(type: Checkstyle) {
    dependsOn subprojects.checkstyleMain
}

task deleteRelease(type: Delete) {
    //delete rootProject.rootDir.absolutePath + '/adorApp-application/release'
}

def releaseCopySpec = project.copySpec {
   from(rootProject.rootDir.absolutePath + '/adorApp-application/modules/adorApp-engine/') {
     include 'adorApp.conf.properties'
     include 'readme.txt'
   }
    from(rootProject.rootDir.absolutePath + '/config/'){
        include 'security/adorApp.cer'
        include 'security/adorApp.keystore'
        include 'security/little_register.xlsx'
    }
    from(rootProject.rootDir.absolutePath + '/adorApp-application/modules/adorApp-engine/build/libs/') {
     include "adorApp-$adorAppVersion.$buildNumber" + '.jar'
   }
}

task copyToRelease(type: Copy) {
    dependsOn deleteRelease
    into rootProject.rootDir.absolutePath + '/release'
    with releaseCopySpec
}

task release(type: Zip) {
    dependsOn copyToRelease
    archiveName = "adorApp-application-$adorAppVersion.$buildNumber"+'.zip'
    from rootProject.rootDir.absolutePath + '/release'
}

subprojects {

    repositories {
        maven { url "https://repository.jboss.org/nexus/content/groups/public" }
    }
  
    dependencies {
        compile(group: 'org.springframework', name: 'spring-context', version:'5.0.12.RELEASE') {
            exclude(module: 'commons-logging')
        }
	    compile group: 'org.springframework', name: 'spring-webmvc', version:'5.0.12.RELEASE'
	    compile group: 'org.aspectj', name: 'aspectjrt', version:'1.6.9'
        compile group: 'org.postgresql', name: 'postgresql', version:'42.2.12.jre7'
	    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
	    testCompile(group: 'org.testng', name: 'testng', version:'6.8.5') {
            exclude(module: 'junit')
        }
    }
    
    test.useTestNG()
}


